#tifoji @github/discord

declare lower;
declare once_per_bar;

input symbol = "VIX";
input suffix = "W"; #hint suffix: Add W for weeklies.
input expirationDate = 230823; #hint expirationDate: Update to target expiration date as needed. 
input Strike_Spread = 1; #hint Strike_Spread: Spread between successive strikes
input userK = 16;  #hint userK: Manual target strike. 
input useATM = yes;   #hint useATM: If set to yes, this overrides userK and sets K to the ATM Strike

def year = Floor(expirationDate / 10000);
def month = Floor((expirationDate - (year * 10000)) / 100);
def day = expirationDate % 100;
def isRTH = RegularTradingStart(GetYYYYMMDD()) <= GetTime() and RegularTradingEnd(GetYYYYMMDD()) > GetTime();
def S =  if(isRTH) then close(symbol=symbol) else close(symbol=symbol,aggregationPeriod.DAY,PriceType.LAST) ;

def adjusted_Strike_Spread = if S < 15 then 0.5 else Strike_Spread;

def ATM = if S >= Round(S / adjusted_Strike_Spread, 0) * adjusted_Strike_Spread
          then Round(S / adjusted_Strike_Spread, 0) * adjusted_Strike_Spread
          else (Round(S / adjusted_Strike_Spread, 0) * adjusted_Strike_Spread) - adjusted_Strike_Spread;

def K = if useATM then ATM else userK;

#ATM Strike
def ATMCV = if !IsNaN(volume("." + symbol + suffix + AsPrice(expirationDate) + "C" + AsPrice(ATM)))  
            then volume("." + symbol + suffix + AsPrice(expirationDate) + "C" + AsPrice(ATM)) else 0;

def ATMPV = if !IsNaN(volume("." + symbol + suffix + AsPrice(expirationDate) + "P" + AsPrice(ATM))) 
            then volume("." + symbol + suffix + AsPrice(expirationDate) + "P" + AsPrice(ATM)) else 0;

# Cumulative totals through RTH progression
def Active = GetDay() == GetLastDay(); #Today
def cumulativeCV = if Active and !Active[1] then ATMCV else if Active then cumulativeCV[1] + ATMCV else cumulativeCV[1];
def cumulativePV = if Active and !Active[1] then ATMPV else if Active then cumulativePV[1] + ATMPV else cumulativePV[1];

# Labels
AddLabel(yes,(if month == 1 then "JAN"
else if month == 2 then "FEB"
else if month == 3 then "MAR"
else if month == 4 then "APR"
else if month == 5 then "MAY"
else if month == 6 then "JUN"
else if month == 7 then "JUL"
else if month == 8 then "AUG"
else if month == 9 then "SEP"
else if month == 10 then "OCT"
else if month == 11 then "NOV"
else "DEC") + " " + day + " " , CreateColor(153,153,255));

AddLabel(yes, "VIX Close: " + AsText(S), color.red);
AddLabel(yes, "ATM (" + ATM + ") P/C Ratio: " + AsText(ATMPV / ATMCV), color.white);
AddLabel(yes, "Current ATM Call Volume (" + ATMCV + "): ", color.white);
AddLabel(yes, "Current ATM Put Volume (" + ATMPV + "): ", color.white);
AddLabel(yes, "Total ATM Call Volume: " + cumulativeCV, color.white);
AddLabel(yes, "Total ATM Put Volume: " + cumulativePV, color.white);

#plots
def ATM_PCR = ATMPV / ATMCV;
plot PCR_Histogram = ATM_PCR;
PCR_Histogram.SetPaintingStrategy(PaintingStrategy.HISTOGRAM);
PCR_Histogram.SetDefaultColor(Color.BLUE);
PCR_Histogram.AssignValueColor(if ATM_PCR > 1 then Color.RED else Color.GREEN);

plot ZeroLine = 0;
ZeroLine.SetDefaultColor(Color.WHITE);
Zeroline.SetStyle(Curve.FIRM);
ZeroLine.HideTitle();
ZeroLine.HideBubble();

plot positiveline = 25;
positiveline.SetDefaultColor(Color.RED);
positiveline.SetStyle(Curve.LONG_DASH);
positiveline.HideTitle();
